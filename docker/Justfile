set positional-arguments := true
set shell := ["bash", "-c"]

# Configuration
ROOT := `git rev-parse --show-toplevel 2>/dev/null || echo "."`
TAG := `git rev-parse --short HEAD 2>/dev/null || echo "dev"`
VERSION := `git describe --tags --always --dirty 2>/dev/null || echo "dev"`
GIT_COMMIT := `git rev-parse HEAD 2>/dev/null || echo "unknown"`
DOCKERFILE := ROOT + "/docker/Dockerfile"

# Build Docker image for local development
build:
    @echo "Building Docker image..."
    @docker build -f {{ DOCKERFILE }} \
        --build-arg VERSION={{ VERSION }} \
        --build-arg GIT_COMMIT={{ GIT_COMMIT }} \
        -t cachew:local \
        {{ ROOT }}
    @echo "✓ Built cachew:local"

# Build multi-arch Docker image (builds for amd64 + arm64, stays in cache)
build-multi:
    @docker buildx create --use --driver docker-container --driver-opt image=moby/buildkit:v0.17.3 --name multi-arch-cachew 2>/dev/null || docker buildx use multi-arch-cachew
    @echo "Building multi-arch image..."
    @docker buildx build \
        --platform linux/amd64,linux/arm64 \
        -f {{ DOCKERFILE }} \
        --build-arg VERSION={{ VERSION }} \
        --build-arg GIT_COMMIT={{ GIT_COMMIT }} \
        -t cachew:{{ TAG }} \
        {{ ROOT }}
    @echo "✓ Built multi-arch image (in cache)"

# Run in Docker (usage: just docker run [log_level])
run log_level="info":
    @just build
    @echo "→ Starting cachew at http://localhost:8080 (log-level={{ log_level }})"
    @docker run --rm -it -p 8080:8080 -v {{ ROOT }}/state:/app/state --name cachew cachew:local --log-level={{ log_level }}

# Clean up Docker images
clean:
    @echo "Cleaning Docker images..."
    @docker rmi cachew:local 2>/dev/null || true
    @docker rmi cachew:{{ TAG }} 2>/dev/null || true
    @echo "✓ Cleaned"
